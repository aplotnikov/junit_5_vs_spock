buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.1'
    }
}

plugins {
    id 'java'
    id 'groovy'

    id 'project-report'
    id 'build-dashboard'

    id 'pmd'
    id 'findbugs'
    id 'checkstyle'
    id 'codenarc'
}

apply plugin: 'org.junit.platform.gradle.plugin'
apply from: 'config/static-code-analyze.gradle'

repositories {
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

jar {
    baseName = 'junit_5_vs_spock'
    group = 'io.github.aplotnikov'
    version = '0.0.1-SNAPSHOT'
}

ext.junitJupiterVersion = '5.0.1'
ext.junitVintageVersion = '4.12.1'

dependencies {
    compile 'io.vavr:vavr:0.9.1'

    testCompile 'nl.jqno.equalsverifier:equalsverifier:2.2.2'

    // Spock + Groovy dependency
    testCompile 'org.codehaus.groovy:groovy-all:2.4.12',
            'org.spockframework:spock-core:1.1-groovy-2.4'
    // Needs for launching Spock test into JUnit 5 platform
    testRuntime 'org.junit.vintage:junit-vintage-engine:4.12.1'

    // JUnit 5 dependency
    testCompile "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}",
            "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    // To avoid compiler warnings about @API annotations in JUnit code
    testCompileOnly 'org.apiguardian:apiguardian-api:1.0.0'

    // JUnit 5 utilities
    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile 'org.awaitility:awaitility:3.0.0'
}

junitPlatform {
    filters {
        tags {
//            Configuration of enabling and disabling tests by tag name
//            include 'fast', 'smoke'
//            exclude 'slow'
            exclude 'failed'
        }

        includeClassNamePatterns '.*Test', '.*Spec'
    }
}